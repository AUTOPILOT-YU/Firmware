#!nsh
#
# PX4FMU startup script.

#
# Default to auto-start mode.  An init script on the microSD card
# can change this to prevent automatic startup of the flight script.
#
set MODE autostart

set RC_FILE /fs/microsd/etc/rc.txt
set CONFIG_FILE /fs/microsd/etc/config.txt
set EXTRAS_FILE /fs/microsd/etc/extras.txt

set TUNE_OUT_ERROR ML<<CP4CP4CP4CP4CP4

#
# Try to mount the microSD card.
#
echo "[init] Looking for microSD..."
if mount -t vfat /dev/mmcsd0 /fs/microsd
then
	set LOG_FILE /fs/microsd/bootlog.txt
	echo "[init] microSD card mounted at /fs/microsd"
	# Start playing the startup tune
	tone_alarm start
else
	set LOG_FILE /dev/null
	echo "[init] No microSD card found"
	# Play SOS
	tone_alarm error
fi

#
# Look for an init script on the microSD card.
# Disable autostart if the script found.
#
if [ -f $RC_FILE ]
then
	echo "[init] Executing init script: $RC_FILE"
	sh $RC_FILE
	set MODE custom
else
	echo "[init] Init script not found: $RC_FILE"
fi
	#
	# Start CDC/ACM serial driver
	#
	sercon
	
	#
	# Start the ORB (first app to start)
	#
	uorb start
	
	#
	# Load parameters
	#
	set PARAM_FILE /fs/microsd/params
	if mtd start
	then
		set PARAM_FILE /fs/mtd_params
	fi
	
	param select $PARAM_FILE
	if param load
	then
		echo "[init] Parameters loaded: $PARAM_FILE"
	else
		echo "[init] ERROR: Parameters loading failed: $PARAM_FILE"
	fi
	
	#
	# Start system state indicator
	#
	if rgbled start
	then
		echo "[init] Using external RGB Led"
	else
		if blinkm start
		then
			echo "[init] Using blinkm"
			blinkm systemstate
		fi
	fi

	sdlog2 start -r 100 -e -b 16

	nshterm /dev/ttyACM0 &

	px4io start
	mixer load /dev/pwm_output /etc/mixers/IO_pass.mix
	pwm failsafe -c 8 -p 1000
	sleep 1
	pwm arm
	tests rc &
	pwm test -c 12345678 -p 1700

# End of autostart
fi
